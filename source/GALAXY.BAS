100 SCREEN1,2,0:WIDTH32:COLOR15,1,1:KEYOFF:DEFINTA-Z:CLEAR400:DIMST$(4),SX(4),SV(4),C(4),D(2):R=RND(-TIME)
110 GOSUB2000
120 C(1)=&H40:C(2)=&H70:C(3)=&H70:C(4)=&H40:D(1)=9:D(2)=6:HI=765
130 X=88:Y=176:VX=0:VY=0:T=1:FU=1000:SC=0:LF=3:RD=1
140 GOSUB1000:GOSUB1100
150 ' ***** STAGE INIT *****
160 FORI=0TO4:A$="":S$=MID$("ahp",I\2+1,1):IFIMOD2=0THENSX(I)=0:SV(I)=1:S$=S$+MID$("biq",I\2+1,1)ELSESX(I)=25:SV(I)=-1:S$=MID$("cjr",I\2+1,1)+S$
170 FORJ=0TO12:IFINT(RND(1)*(3+I\2))=0THENA$=A$+S$ELSEA$=A$+"  "
180 NEXT:ST$(I)=A$+A$:NEXT
200 ' ***** MAIN *****
210 Z=TIME:T=TMOD2+1
220 LOCATE0,5:PRINTMID$(ST$(0),SX(0)+1,25);
230 SX(0)=SX(0)+SV(0):IFSX(0)>25THENSX(0)=0
240 LOCATE0,8:PRINTMID$(ST$(1),SX(1)+1,25);
250 SX(1)=SX(1)+SV(1):IFSX(1)<0THENSX(1)=25
260 LOCATE0,11:PRINTMID$(ST$(2),SX(2)+1,25);
270 SX(2)=SX(2)+SV(2):IFSX(2)>25THENSX(2)=0
280 LOCATE0,14:PRINTMID$(ST$(3),SX(3)+1,25);
290 SX(3)=SX(3)+SV(3):IFSX(3)<0THENSX(3)=25
300 LOCATE0,17:PRINTMID$(ST$(4),SX(4)+1,25);
310 SX(4)=SX(4)+SV(4):IFSX(4)>25THENSX(4)=0
320 ON(STICK(0)+STICK(1))GOTO360,360,330,360,360,360,340,360:GOTO350
330 VX=VX+4:IFVX>8THENVX=8:GOTO350ELSE360
340 VX=VX-4:IFVX<-8THENVX=-8:GOTO350ELSE360
350 VX=VX+SGN(0-VX)*4
360 IFSTRIG(0)+STRIG(1)ANDVY>-8THENVY=VY-4ELSEIFVY<0THENVY=VY+4
370 X=X+VX:Y=Y+VY
380 PUTSPRITE0,(X-4,Y),14,0:IFVYTHENPUTSPRITE1,(X-4,Y+T),D(T),1ELSEPUTSPRITE1,(-16,-16),0,1
390 VPOKE&H2017,C(T):VPOKE&H2018,C(T+2):LOCATE0,0:PRINTTIME-Z;:GOTO 210
1000 ' ***** SCREEN *****
1010 FORI=0TO23:LOCATE25,I:PRINT"ｱ";:NEXT
1020 LOCATE26,0:PRINT"======";:LOCATE26,1:PRINT"DANGER";:LOCATE26,2:PRINT" ZONE";:LOCATE26,3:PRINT"======";
1030 LOCATE26,6:PRINT"TOP";:LOCATE26,9:PRINT"1UP";:LOCATE26,12:PRINT"FUEL";
1040 LOCATE26,20:PRINTUSING"LEFT #";LF;:LOCATE26,22:PRINTUSING"RD. ##";RD;
1050 FORI=0TO19:FORJ=1TO2:LOCATEINT(RND(1)*25),INT(RND(1)*24):PRINTMID$("ｸﾀ",J,1);:NEXT:NEXT
1060 RETURN
1100 ' ***** INFORMATION *****
1110 IFSC>HITHENHI=SC
1120 LOCATE26,7:PRINTUSING"#####0";HI;
1130 LOCATE26,10:PRINTUSING"#####0";SC;
1140 LOCATE28,13:PRINTUSING"####";FU;:RETURN
2000 ' ***** CHARACTER DEFINE *****
2010 READA$:IFA$<>"*"THENREADB$,C$:FORJ=0TO7:VPOKEASC(A$)*8+J,VAL("&H"+MID$(B$,J*2+1,2)):NEXT:VPOKE&H2000+ASC(A$)\8,VAL("&H"+C$):GOTO2010
2020 S=BASE(9)
2030 READR$:IFR$="*"THENRETURNELSEVPOKES,VAL("&H"+R$):S=S+1:GOTO2030
2100 ' ***** PCG DATA *****
2110 DATA a,3C7EEFBFEFDD6E3C,60,b,0000600070006000,60,c,000006000E000600,60
2120 DATA h,3C7EEFBFEFDD6E3C,D0,i,0000600070006000,D0,j,000006000E000600,D0
2130 DATA p,3C7EEFBFEFDD6E3C,70,q,0000600070006000,70,r,000006000E000600,70
2140 DATA ｱ,2424242424242424,40,ｸ,0000001000000000,40,ﾀ,0000000000000001,70
2150 DATA *
2200 ' ***** SPRITE DATA *****
2210 DATA 01,01,03,03,06,07,02,03,07,06,0C,08,00,00,00,00
2220 DATA 00,00,80,80,C0,C0,80,80,C0,C0,60,20,00,00,00,00
2230 DATA 00,00,00,00,00,00,00,00,00,00,01,03,03,05,00,09
2240 DATA 00,00,00,00,00,00,00,00,00,00,00,80,80,40,00,20
2250 DATA *
