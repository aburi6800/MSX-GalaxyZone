100 SCREEN1,2,0:WIDTH32:COLOR15,1,1:KEYOFF:DEFINTA-Z:CLEAR400:DIMST$(4),SX(4),SV(4),C(6),D(192):R=RND(-TIME):GOSUB2000
110 HI=765:D(97)=1:D(104)=1:D(112)=1
115 SOUND7,177:SOUND6,31
120 X=88:Y=176:VX=0:VY=0:T=1:FU=999:UX=0:UV=0:SC=0:LF=2:RD=1:GOSUB1500:GOSUB1000
130 GOSUB700
200 ' ***** MAIN *****
210 T=TMOD2+1
220 LOCATE0,5:PRINTMID$(ST$(0),SX(0)+1,25);
230 SX(0)=SX(0)+SV(0):IFSX(0)>25THENSX(0)=0
240 LOCATE0,8:PRINTMID$(ST$(1),SX(1)+1,25);
250 SX(1)=SX(1)+SV(1):IFSX(1)<0THENSX(1)=25
260 LOCATE0,11:PRINTMID$(ST$(2),SX(2)+1,25);
270 SX(2)=SX(2)+SV(2):IFSX(2)>25THENSX(2)=0
280 LOCATE0,14:PRINTMID$(ST$(3),SX(3)+1,25);
290 SX(3)=SX(3)+SV(3):IFSX(3)<0THENSX(3)=25
300 LOCATE0,17:PRINTMID$(ST$(4),SX(4)+1,25);
310 SX(4)=SX(4)+SV(4):IFSX(4)>25THENSX(4)=0
320 ON(STICK(0)+STICK(1))GOTO360,360,330,360,360,360,340,360: GOTO350
330 VX=VX+2:IFVX>6THENVX=6
332 X=X+VX:IFX>192THENX=192
334 GOTO360
340 VX=VX-2:IFVX<-6THENVX=-6
342 X=X+VX:IFX<0THENX=0
344 GOTO360
350 VX=VX+SGN(0-VX)*2
360 ON-(STRIG(0)+STRIG(1))GOTO370:GOTO380
370 IFVY>-6THENVY=VY-2:FU=FU-30
375 GOTO390
380 IFVY<0THENVY=VY+2
390 Y=Y+VY:SOUND8,ABS(VY)*2:SOUND6,31+VY:IFY<16GOTO500
400 PUTSPRITE0,(X-4,Y-4),14,0:IFVYTHENPUTSPRITE1,(X-4,Y-4+T),C(T+4),1ELSEPUTSPRITE1,(-16,-16),0,1
410 A=&H1800+X\8+(y\8)*32:OND(VPEEK(A))+D(VPEEK(A+32))GOTO900
420 VPOKE&H2017,C(T):VPOKE&H2018,C(T+2)
430 FU=FU-2:IFFU<0THENFU=0
440 LOCATE29,13:PRINTUSING"###";FU;:
450 IFFU>0GOTO210ELSE950
500 ' ***** CLEAR CHECK *****
510 IFABS(X-UX)>14THEN930
520 PUTSPRITE0,(-16,-16),0,0:PUTSPRITE1,(-16,-16),0,0
530 FORI=0TO20:SOUND6,31:SOUND8,16:SOUND11,50:SOUND12,50:SOUND13,0
535 PUTSPRITE2,(UX-16-I\5,IMOD2+I\5),6,3:PUTSPRITE3,(UX+I\5,IMOD2+I\5),6,4
540 PUTSPRITE4+IMOD5,(UX+INT(RND(1)*32)-16,INT(RND(1)*16)),8,2:FORJ=0TO9:NEXT:NEXT
550 LOCATE5,8:PRINT"MISSION COMPLETE!":B=FU/2:FORI=0TO499:NEXT
560 LOCATE6,11:PRINTUSING"BONUS ###0 PTS.";B:SC=SC+B:FORI=0TO499:NEXT
570 RD=RD+1:GOSUB1100:GOTO130
700 ' ***** STAGE INIT *****
710 P1=(RDMOD9)\2+2:IFP1>5THENP1=5
720 P2=1:IFRD>8THENP2=2
730 FORI=0TO4:A$="":S$=MID$("ahp",I\2+1,1):IFIMOD2=0THENSX(I)=0:SV(I)=P2:S$=S$+MID$("biq",I\2+1,1)ELSESX(I)=25:SV(I)=-1*P2:S$=MID$("cjr",I\2+1,1)+S$
740 FORJ=0TO12:IFINT(RND(1)*(3+I\2))=0ANDI<P1THENA$=A$+S$ELSEA$=A$+"  "
750 NEXT:ST$(I)=A$+A$:NEXT
760 X=88:Y=176:VX=0:VY=0:T=1:FU=999:UX=INT(RND(1)*160)+24
770 FORI=4TO8:PUTSPRITEI,(-16,-16),0,0:NEXT:PUTSPRITE2,(UX-16,0),11,3:PUTSPRITE3,(UX,0),11,4
780 GOSUB1100:RETURN
900 ' ***** MISS1 *****
910 PUTSPRITE0,(X-4,Y),6,2:PUTSPRITE1,(-16,-16),0,1:SOUND6,15:SOUND8,16:SOUND11,50:SOUND12,50:SOUND13,0
920 FORI=0TO1999:NEXT:IFLF=0GOTO970ELSELF=LF-1:GOSUB1100:GOTO130
930 ' ***** MISS2 *****
940 LOCATE4,8:PRINT"MISSION FAILED...":GOTO900
950 ' ***** MISS3 *****
960 LOCATE7,5:PRINT"FUEL EMPTY.":GOTO930
970 ' ***** GAMEOVER *****
980 FORI=0TO4:PUTSPRITEI,(-16,-16),0,0:NEXT:FORI=0TO24:LOCATE0,I:PRINTSPACE$(25);:NEXT
990 LOCATE8,8:PRINT"GAME OVER":FORI=0TO2999:NEXT:GOTO120
1000 ' ***** SCREEN *****
1010 FORI=0TO23:LOCATE25,I:PRINT"ｱ";:NEXT
1020 LOCATE26,0:PRINT"ｲｲｲｲｲｲ";:LOCATE26,1:PRINT"GALAXY";:LOCATE26,2:PRINT" ZONE";:LOCATE26,3:PRINT"ｲｲｲｲｲｲ";
1030 LOCATE26,6:PRINT"TOP";:LOCATE26,9:PRINT"1UP";:LOCATE26,12:PRINT"FUEL";
1040 LOCATE26,20:PRINTUSING"LEFT #";LF;:LOCATE26,22:PRINTUSING"RD. ##";RD;
1050 FORI=0TO19:FORJ=1TO2:LOCATEINT(RND(1)*25),INT(RND(1)*24):PRINTMID$("ｸﾀ",J,1);:NEXT:NEXT
1060 RETURN
1100 ' ***** INFORMATION *****
1110 IFSC>HITHENHI=SC
1120 LOCATE26,7:PRINTUSING"#####0";HI;
1130 LOCATE26,10:PRINTUSING"#####0";SC;
1150 LOCATE26,20:PRINTUSING"LEFT #";LF;:LOCATE26,22:PRINTUSING"RD. ##";RD;
1160 RETURN
1500 ' ***** TITLE *****
1510 GOSUB1900:GOSUB1000:GOSUB1100
1520 FORI=1TO11:LOCATE6+I,2:PRINTMID$("GALAXY ZONE",I,1);:FORJ=0TO99:NEXT:NEXT
1530 LOCATE3,5:PRINT"ab":LOCATE3,6:PRINT"jh"
1540 PUTSPRITE2,(16,80),11,3:PUTSPRITE3,(32,80),11,4
1550 LOCATE8,5:PRINT"... ASTEROID":LOCATE12,7:PRINT"MISS."
1560 LOCATE8,11:PRINT"... MOTHER SHIP":LOCATE12,13:PRINT"DESTROY IT!"
1570 LOCATE3,18:PRINT"PUSH SPACE TO START"
1580 LOCATE4,21:PRINT"(C)ABURI6800 2021"
1590 IFSTRIG(0)+STRIG(1)=0GOTO1590ELSEGOSUB1900:RETURN 
1900 ' ***** SCREEN CLEAR *****
1910 FORI=0TO4:PUTSPRITEI,(-16,-16),0,0:NEXT:FORI=0TO24:LOCATE0,I:PRINTSPACE$(25);:NEXT:RETURN
2000 ' ***** DATA SET *****
2010 READA$:IFA$<>"*"THENREADB$,C$:FORJ=0TO7:VPOKEASC(A$)*8+J,VAL("&H"+MID$(B$,J*2+1,2)):NEXT:VPOKE&H2000+ASC(A$)\8,VAL("&H"+C$):GOTO2010
2020 S=BASE(9)
2030 READR$:IFR$<>"*"THENVPOKES,VAL("&H"+R$):S=S+1:GOTO2030
2040 FORI=1TO6:READA$:C(I)=VAL("&H"+A$):NEXT
2050 RETURN
2100 ' ***** PCG DATA *****
2110 DATA a,3C7EEFBFEFDD6E3C,60,b,0000600070006000,60,c,000006000E000600,60
2120 DATA h,3C7EEFBFEFDD6E3C,D0,i,0000600070006000,D0,j,000006000E000600,D0
2130 DATA p,3C7EEFBFEFDD6E3C,70,q,0000600070006000,70,r,000006000E000600,70
2140 DATA ｱ,2424242424242424,40,ｲ,0000FF00FF000000,40
2150 DATA ｸ,0000001000000000,40,ﾀ,0000000000000001,70
2160 DATA *
2200 ' ***** SPRITE DATA *****
2210 DATA 01,01,03,03,06,07,02,03,07,06,0C,08,00,00,00,00
2220 DATA 00,00,80,80,C0,C0,80,80,C0,C0,60,20,00,00,00,00
2230 '
2240 DATA 00,00,00,00,00,00,00,00,00,00,01,03,03,05,00,09
2250 DATA 00,00,00,00,00,00,00,00,00,00,00,80,80,40,00,20
2260 '
2270 DATA 11,19,4F,0F,1F,1E,3B,FF,3F,1B,17,3A,21,41,00,00
2280 DATA 00,08,30,F0,61,BE,DC,B8,B0,D0,38,70,08,04,00,00
2290 '
2300 DATA 00,00,00,00,00,00,03,0F,3F,F5,FF,7F,1B,07,01,00
2310 DATA 20,58,85,0A,55,AF,7F,FF,FF,99,FF,FE,FD,3B,E5,FE
2320 '
2330 DATA 04,1A,A1,D8,FE,FF,FF,FF,FF,99,FF,7F,BF,DC,A7,7F
2340 DATA 00,00,00,00,00,00,C0,F0,FC,AF,FF,FE,D8,E0,80,00
2350 DATA *
2500 ' ***** COLOR PATTERN *****
2510 DATA 40,70,70,40,09,06
